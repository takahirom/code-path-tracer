plugins {
  id 'com.android.application' version libs.versions.agp apply false
  id 'com.android.library' version libs.versions.agp apply false
  id 'org.jetbrains.kotlin.android' version libs.versions.kotlin apply false
  id 'org.jetbrains.kotlin.jvm' version libs.versions.kotlin apply false
  id "com.vanniktech.maven.publish" version libs.versions.mavenPublish apply false
}

allprojects {
  // Apply the same Java compatibility for each module
  def javaVersion = libs.versions.javaTarget.get()
  def toolchainVersion = libs.versions.javaToolchain.get()

  def javaTargetVersion = JavaVersion.toVersion(javaVersion)
  def jvmTargetVersion = org.jetbrains.kotlin.gradle.dsl.JvmTarget.@Companion.fromTarget(javaVersion)

  plugins.withId("java") {
    java {
      toolchain {
        languageVersion.set(JavaLanguageVersion.of(toolchainVersion))
      }
    }
  }

  tasks.withType(org.gradle.api.tasks.compile.JavaCompile).configureEach {
    sourceCompatibility = javaTargetVersion
    targetCompatibility = javaTargetVersion
  }

  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    compilerOptions {
      jvmTarget.set(jvmTargetVersion)
      allWarningsAsErrors = false
    }
  }
  
  plugins.withId('com.vanniktech.maven.publish') {
    project.group = "io.github.takahirom.codepathtracer"
    mavenPublishing {
      publishToMavenCentral(com.vanniktech.maven.publish.SonatypeHost.CENTRAL_PORTAL)
//      signAllPublications()
    }

    plugins.withId('maven-publish') {
      publishing {
        repositories {
          maven {
            name = "LocalMaven"
            url = file("${rootProject.buildDir}/localMaven").toURI().toString()
          }
        }
      }
    }
  }
}